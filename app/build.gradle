apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        internal {
            dimension "default"
        }
        normal {
            dimension "default"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /** Rename output file names */
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            // 修改输出的apk文件名
            outputFileName = generateOutputApkFileName(variant.buildType.name, variant.flavorName, defaultConfig.versionName)

            /*移动文件
            output.assemble.doLast {
                copy {
                    from output.outputFile
                    into "${project.buildDir}/outputs/apks"
                }
            }*/
        }
    }

    applicationVariants.all { variant ->
        rootProject.ext.dft.each {
            variant.buildConfigField(it[0], it[1], it[2])
        }
        if (variant.flavorName == "internal") {
            rootProject.ext.internal.each {
                variant.buildConfigField(it[0], it[1], it[2])
            }
        }
    }
}

//重命名Apk文件
static def generateOutputApkFileName(String buildType, String flavorName, String version) {
    String path = "MyRecord-"
    if (!buildType.empty) path += buildType + "-"
    if (!flavorName.empty) path += flavorName + "-"
    if (!version.empty) path += version
    return path + ".apk"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(path: ':database')


    compile rootProject.ext.dependencies.appcompat
    compile rootProject.ext.dependencies.design
    compile rootProject.ext.dependencies.cardview
    compile rootProject.ext.dependencies.glide
    compile rootProject.ext.dependencies.crop
    compile rootProject.ext.dependencies.eventbus

    compile rootProject.ext.dependencies.rxjava
    compile rootProject.ext.dependencies.rxandroid

    compile rootProject.ext.dependencies.okhttp
    compile rootProject.ext.dependencies.gson

    compile rootProject.ext.dependencies.butterknife
    annotationProcessor rootProject.ext.dependencies.butterknife_compiler
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
}
